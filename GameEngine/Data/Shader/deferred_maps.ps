in vec2 texCoords;
in vec3 normal;
in vec3 tangent;
in vec3 bitangent;

layout(location = 0) out vec4 finalColor;
layout(location = 1) out vec3 finalNormal;
layout(location = 2) out vec3 finalSpecular;
layout(location = 3) out vec3 finalEmissive;

uniform sampler2D diffuseMap;
uniform sampler2D normalMap;
uniform sampler2D specularMap;
uniform sampler2D emissiveMap;

void main()
{
    vec4 diffuseTextureColor = texture( diffuseMap, texCoords );
    if( diffuseTextureColor.a < 0.001 )
        discard;

    mat3 TBN = mat3( normalize( tangent ), normalize( bitangent ), normalize( normal ) );

    vec3 tangentSpaceNormal = 2.0 * texture( normalMap, texCoords).rgb - vec3( 1.0 );
    vec3 worldSpaceNormal = TBN * tangentSpaceNormal;

    finalColor = vec4( diffuseTextureColor.rgb, 1.0 );
    finalNormal = normalize( worldSpaceNormal ) * 0.5 + vec3( 0.5, 0.5, 0.5 );
    finalSpecular = vec3( texture( specularMap, texCoords ).r, 0.0, 0.0 );
    finalEmissive = texture( emissiveMap, texCoords ).rgb;
}